# Plan-only tests for the observability of the failures that don't fail a
# Lambda (issue #126 § Alarms and spend): the DLQ alarms fire on each new
# arrival rather than on depth (alarms.tf local.dlq_arrivals_expression), and
# the fetcher's and renderer's answered failures, stuck queues and throttles
# alarm (feeds.tf, reports.tf).
#
# Same mocked providers, overrides and variables as guardrails.tftest.hcl
# (copied, since a test file can't share them): no AWS credentials, no state.
#
#   pnpm check:infra          (bin/check-infra.sh: fmt + validate + test)

mock_provider "aws" {
  # A mocked policy document renders a random string, which IAM/SNS/S3 policy
  # arguments then reject as invalid JSON. Any well-formed document does.
  mock_data "aws_iam_policy_document" {
    defaults = {
      json = "{\"Version\":\"2012-10-17\",\"Statement\":[]}"
    }
  }
  mock_data "aws_availability_zones" {
    defaults = {
      names    = ["af-south-1a", "af-south-1b", "af-south-1c"]
      zone_ids = ["afs1-az1", "afs1-az2", "afs1-az3"]
    }
  }
  mock_data "aws_caller_identity" {
    defaults = {
      account_id = "000000000000"
    }
  }
  mock_data "aws_iam_role" {
    defaults = {
      arn  = "arn:aws:iam::000000000000:role/water-management-deploy"
      name = "water-management-deploy"
      # The bootstrap's trust policy as IAM returns it (single values
      # collapsed to strings) — templates infra/modules/project-baseline.
      assume_role_policy = "{\"Version\":\"2012-10-17\",\"Statement\":[{\"Effect\":\"Allow\",\"Principal\":{\"Federated\":\"arn:aws:iam::000000000000:oidc-provider/token.actions.githubusercontent.com\"},\"Action\":\"sts:AssumeRoleWithWebIdentity\",\"Condition\":{\"StringEquals\":{\"token.actions.githubusercontent.com:aud\":\"sts.amazonaws.com\",\"token.actions.githubusercontent.com:sub\":\"repo:Absence0760/project-water-management:environment:production\"}}}]}"
    }
  }
}

mock_provider "aws" {
  alias = "us_east_1"
  mock_data "aws_iam_policy_document" {
    defaults = {
      json = "{\"Version\":\"2012-10-17\",\"Statement\":[]}"
    }
  }
}

mock_provider "archive" {}
mock_provider "random" {}

# Mocked RDS has no provider logic, so the computed master secret and
# endpoint would stay unknown/empty. Supply what RDS really returns.
override_resource {
  target          = aws_db_instance.main
  override_during = plan
  values = {
    address = "water-management.abc123.af-south-1.rds.amazonaws.com"
    port    = 5432
    master_user_secret = [{
      secret_arn    = "arn:aws:secretsmanager:af-south-1:000000000000:secret:rds!db-00000000-0000-0000-0000-000000000000-AbCdEf"
      kms_key_id    = "arn:aws:kms:af-south-1:000000000000:key/00000000-0000-0000-0000-000000000000"
      secret_status = "active"
    }]
  }
}

override_resource {
  target          = aws_acm_certificate.frontend
  override_during = plan
  values = {
    arn = "arn:aws:acm:us-east-1:000000000000:certificate/00000000-0000-0000-0000-000000000000"
    domain_validation_options = [{
      domain_name           = "water-management.jaredhoward.com"
      resource_record_name  = "_0123abcd.water-management.jaredhoward.com."
      resource_record_type  = "CNAME"
      resource_record_value = "_4567efab.acm-validations.aws."
    }]
  }
}

# Easy DKIM returns three tokens only after create; the DKIM records index them.
override_resource {
  target          = aws_sesv2_email_identity.domain
  override_during = plan
  values = {
    arn = "arn:aws:ses:af-south-1:000000000000:identity/water-management.jaredhoward.com"
    dkim_signing_attributes = {
      next_signing_key_length = "RSA_2048_BIT"
      tokens                  = ["tok1", "tok2", "tok3"]
    }
  }
}

override_resource {
  target          = aws_sesv2_configuration_set.main
  override_during = plan
  values = {
    arn = "arn:aws:ses:af-south-1:000000000000:configuration-set/water-management"
  }
}

# Known ARNs / IDs so wiring (WAF → distribution, header policy → behaviour,
# alarm → topic) can be asserted by equality at plan time.
override_resource {
  target          = aws_wafv2_web_acl.frontend
  override_during = plan
  values = {
    arn = "arn:aws:wafv2:us-east-1:000000000000:global/webacl/water-management-frontend-acl/00000000-0000-0000-0000-000000000000"
  }
}

override_resource {
  target          = aws_cloudfront_response_headers_policy.site
  override_during = plan
  values = {
    id = "site-headers-policy-id"
  }
}

override_resource {
  target          = aws_cloudfront_response_headers_policy.api
  override_during = plan
  values = {
    id = "api-headers-policy-id"
  }
}

override_resource {
  target          = aws_iam_role.lambda
  override_during = plan
  values = {
    id  = "water-management-backend-lambda"
    arn = "arn:aws:iam::000000000000:role/water-management-backend-lambda"
  }
}

override_resource {
  target          = aws_s3_bucket.frontend
  override_during = plan
  values = {
    arn = "arn:aws:s3:::water-management-frontend-000000000000"
  }
}

override_resource {
  target          = aws_cloudfront_distribution.frontend
  override_during = plan
  values = {
    arn = "arn:aws:cloudfront::000000000000:distribution/E0000000000000"
    id  = "E0000000000000"
  }
}

override_resource {
  target          = aws_lambda_function.backend
  override_during = plan
  values = {
    arn = "arn:aws:lambda:af-south-1:000000000000:function:water-management-backend"
  }
}

override_resource {
  target          = aws_lambda_function.migrate
  override_during = plan
  values = {
    arn = "arn:aws:lambda:af-south-1:000000000000:function:water-management-migrate"
  }
}

override_resource {
  target          = aws_lambda_function.worker
  override_during = plan
  values = {
    arn           = "arn:aws:lambda:af-south-1:000000000000:function:water-management-worker"
    function_name = "water-management-worker"
  }
}

override_resource {
  target          = aws_iam_role.worker_lambda
  override_during = plan
  values = {
    id  = "water-management-worker-lambda"
    arn = "arn:aws:iam::000000000000:role/water-management-worker-lambda"
  }
}

override_resource {
  target          = aws_sqs_queue.jobs
  override_during = plan
  values = {
    arn = "arn:aws:sqs:af-south-1:000000000000:water-management-jobs"
    url = "https://sqs.af-south-1.amazonaws.com/000000000000/water-management-jobs"
  }
}

override_resource {
  target          = aws_cloudwatch_event_rule.worker_tick
  override_during = plan
  values = {
    arn = "arn:aws:events:af-south-1:000000000000:rule/water-management-worker-tick"
  }
}

override_resource {
  target          = aws_security_group.worker_lambda
  override_during = plan
  values = {
    id = "sg-0000000000worker"
  }
}

# Known IDs for the rest of the security groups and the route table, so the
# network run can assert which group a rule, Lambda or DB points at.
override_resource {
  target          = aws_security_group.api_lambda
  override_during = plan
  values = {
    id = "sg-000000000000api"
  }
}

override_resource {
  target          = aws_security_group.migrate_lambda
  override_during = plan
  values = {
    id = "sg-00000000migrate"
  }
}

override_resource {
  target          = aws_security_group.rds
  override_during = plan
  values = {
    id = "sg-000000000000rds"
  }
}

override_resource {
  target          = aws_security_group.vpce
  override_during = plan
  values = {
    id = "sg-00000000000vpce"
  }
}

override_resource {
  target          = aws_security_group.vpce_sqs
  override_during = plan
  values = {
    id = "sg-000000000vpcesqs"
  }
}

override_resource {
  target          = aws_route_table.private
  override_during = plan
  values = {
    id = "rtb-00000000private"
  }
}

override_resource {
  target          = aws_sqs_queue.jobs_dlq
  override_during = plan
  values = {
    arn = "arn:aws:sqs:af-south-1:000000000000:water-management-jobs-dlq"
    url = "https://sqs.af-south-1.amazonaws.com/000000000000/water-management-jobs-dlq"
  }
}

# Data feeds (feeds.tf, WP-2.10)
override_resource {
  target          = aws_sqs_queue.fetch_requests
  override_during = plan
  values = {
    arn = "arn:aws:sqs:af-south-1:000000000000:water-management-fetch-requests"
    url = "https://sqs.af-south-1.amazonaws.com/000000000000/water-management-fetch-requests"
  }
}

override_resource {
  target          = aws_sqs_queue.fetch_requests_dlq
  override_during = plan
  values = {
    arn = "arn:aws:sqs:af-south-1:000000000000:water-management-fetch-requests-dlq"
    url = "https://sqs.af-south-1.amazonaws.com/000000000000/water-management-fetch-requests-dlq"
  }
}

override_resource {
  target          = aws_sqs_queue.ingest_results
  override_during = plan
  values = {
    arn = "arn:aws:sqs:af-south-1:000000000000:water-management-ingest-results"
    url = "https://sqs.af-south-1.amazonaws.com/000000000000/water-management-ingest-results"
  }
}

override_resource {
  target          = aws_sqs_queue.ingest_results_dlq
  override_during = plan
  values = {
    arn = "arn:aws:sqs:af-south-1:000000000000:water-management-ingest-results-dlq"
    url = "https://sqs.af-south-1.amazonaws.com/000000000000/water-management-ingest-results-dlq"
  }
}

override_resource {
  target          = aws_lambda_function.fetcher
  override_during = plan
  values = {
    arn           = "arn:aws:lambda:af-south-1:000000000000:function:water-management-fetcher"
    function_name = "water-management-fetcher"
  }
}

override_resource {
  target          = aws_iam_role.fetcher_lambda
  override_during = plan
  values = {
    id  = "water-management-fetcher-lambda"
    arn = "arn:aws:iam::000000000000:role/water-management-fetcher-lambda"
  }
}

# Server-side reports (reports.tf, WP-2.15 Phase B)
override_resource {
  target          = aws_sqs_queue.render_requests
  override_during = plan
  values = {
    arn = "arn:aws:sqs:af-south-1:000000000000:water-management-render-requests"
    url = "https://sqs.af-south-1.amazonaws.com/000000000000/water-management-render-requests"
  }
}

override_resource {
  target          = aws_sqs_queue.render_requests_dlq
  override_during = plan
  values = {
    arn = "arn:aws:sqs:af-south-1:000000000000:water-management-render-requests-dlq"
    url = "https://sqs.af-south-1.amazonaws.com/000000000000/water-management-render-requests-dlq"
  }
}

override_resource {
  target          = aws_sqs_queue.render_results
  override_during = plan
  values = {
    arn = "arn:aws:sqs:af-south-1:000000000000:water-management-render-results"
    url = "https://sqs.af-south-1.amazonaws.com/000000000000/water-management-render-results"
  }
}

override_resource {
  target          = aws_sqs_queue.render_results_dlq
  override_during = plan
  values = {
    arn = "arn:aws:sqs:af-south-1:000000000000:water-management-render-results-dlq"
    url = "https://sqs.af-south-1.amazonaws.com/000000000000/water-management-render-results-dlq"
  }
}

override_resource {
  target          = aws_s3_bucket.reports
  override_during = plan
  values = {
    arn    = "arn:aws:s3:::water-management-reports-000000000000"
    bucket = "water-management-reports-000000000000"
    # The /reports/* origin (s3_cloudfront.tf).
    bucket_regional_domain_name = "water-management-reports-000000000000.s3.af-south-1.amazonaws.com"
  }
}

# Report downloads through CloudFront (reports.tf): known values so the key
# pair, key group, OAC and origin request policy can be matched by equality.
# Placeholder text, not a key: nothing is generated in a test.
# One id per trusted public key (the overlap run uses both), so a key-id
# check can tell them apart.
override_resource {
  target          = aws_cloudfront_public_key.report_downloads["2026-09"]
  override_during = plan
  values = {
    id = "K2026090000000A"
  }
}

override_resource {
  target          = aws_cloudfront_public_key.report_downloads["2027-03"]
  override_during = plan
  values = {
    id = "K2027030000000B"
  }
}

override_resource {
  target          = aws_cloudfront_key_group.report_downloads
  override_during = plan
  values = {
    id = "report-downloads-key-group-id"
  }
}

override_resource {
  target          = aws_cloudfront_origin_access_control.reports
  override_during = plan
  values = {
    id = "reports-oac-id"
  }
}

# Evidence pack PDFs (packs.tf, 116_pack_render)
override_resource {
  target          = aws_s3_bucket.packs
  override_during = plan
  values = {
    arn    = "arn:aws:s3:::water-management-packs-000000000000"
    bucket = "water-management-packs-000000000000"
    # The /packs/* origin (s3_cloudfront.tf).
    bucket_regional_domain_name = "water-management-packs-000000000000.s3.af-south-1.amazonaws.com"
  }
}

override_resource {
  target          = aws_cloudfront_origin_access_control.packs
  override_during = plan
  values = {
    id = "packs-oac-id"
  }
}

override_resource {
  target          = aws_cloudfront_origin_request_policy.report_downloads
  override_during = plan
  values = {
    id = "report-downloads-origin-request-policy-id"
  }
}

override_resource {
  target          = aws_ecr_repository.renderer
  override_during = plan
  values = {
    arn            = "arn:aws:ecr:af-south-1:000000000000:repository/water-management-renderer"
    repository_url = "000000000000.dkr.ecr.af-south-1.amazonaws.com/water-management-renderer"
  }
}

override_resource {
  target          = aws_iam_role.renderer_lambda
  override_during = plan
  values = {
    id  = "water-management-renderer-lambda"
    arn = "arn:aws:iam::000000000000:role/water-management-renderer-lambda"
  }
}

override_resource {
  target          = aws_security_group.vpce_ses
  override_during = plan
  values = {
    id = "sg-00000000000vpceses"
  }
}

override_resource {
  target          = aws_sns_topic.alerts
  override_during = plan
  values = {
    arn = "arn:aws:sns:af-south-1:000000000000:water-management-prod-alerts"
  }
}

override_resource {
  target          = aws_sns_topic.alerts_us_east_1
  override_during = plan
  values = {
    arn = "arn:aws:sns:us-east-1:000000000000:water-management-prod-alerts"
  }
}

override_resource {
  target          = aws_ce_anomaly_monitor.services
  override_during = plan
  values = {
    arn = "arn:aws:ce::000000000000:anomalymonitor/00000000-0000-0000-0000-000000000000"
  }
}

# SES bounces and complaints → the worker (ses.tf, WP-2.13 follow-up)
override_resource {
  target          = aws_sns_topic.ses_events
  override_during = plan
  values = {
    arn = "arn:aws:sns:af-south-1:000000000000:water-management-ses-events"
  }
}

override_resource {
  target          = aws_sqs_queue.mail_events
  override_during = plan
  values = {
    arn = "arn:aws:sqs:af-south-1:000000000000:water-management-mail-events"
    url = "https://sqs.af-south-1.amazonaws.com/000000000000/water-management-mail-events"
  }
}

override_resource {
  target          = aws_sqs_queue.mail_events_dlq
  override_during = plan
  values = {
    arn = "arn:aws:sqs:af-south-1:000000000000:water-management-mail-events-dlq"
    url = "https://sqs.af-south-1.amazonaws.com/000000000000/water-management-mail-events-dlq"
  }
}

# Runtime secrets (secrets.tf, issue #126): distinct known ARNs, so each
# role's grant and each endpoint statement can be matched to its own secret.
override_resource {
  target          = aws_iam_role.migrate_lambda
  override_during = plan
  values = {
    id  = "water-management-migrate-lambda"
    arn = "arn:aws:iam::000000000000:role/water-management-migrate-lambda"
  }
}

override_resource {
  target          = aws_secretsmanager_secret.runtime["api"]
  override_during = plan
  values = {
    id  = "arn:aws:secretsmanager:af-south-1:000000000000:secret:water-management/runtime/api-AaAaAa"
    arn = "arn:aws:secretsmanager:af-south-1:000000000000:secret:water-management/runtime/api-AaAaAa"
  }
}

override_resource {
  target          = aws_secretsmanager_secret.runtime["worker"]
  override_during = plan
  values = {
    id  = "arn:aws:secretsmanager:af-south-1:000000000000:secret:water-management/runtime/worker-WwWwWw"
    arn = "arn:aws:secretsmanager:af-south-1:000000000000:secret:water-management/runtime/worker-WwWwWw"
  }
}

override_resource {
  target          = aws_secretsmanager_secret.runtime["migrate"]
  override_during = plan
  values = {
    id  = "arn:aws:secretsmanager:af-south-1:000000000000:secret:water-management/runtime/migrate-MmMmMm"
    arn = "arn:aws:secretsmanager:af-south-1:000000000000:secret:water-management/runtime/migrate-MmMmMm"
  }
}

# Distinct version IDs, so matching each Lambda's RUNTIME_SECRET_VERSION to its
# own secret's version can fail.
override_resource {
  target          = aws_secretsmanager_secret_version.runtime["api"]
  override_during = plan
  values = {
    version_id = "aaaaaaaa-0000-0000-0000-000000000001"
  }
}

override_resource {
  target          = aws_secretsmanager_secret_version.runtime["worker"]
  override_during = plan
  values = {
    version_id = "bbbbbbbb-0000-0000-0000-000000000002"
  }
}

override_resource {
  target          = aws_secretsmanager_secret_version.runtime["migrate"]
  override_during = plan
  values = {
    version_id = "cccccccc-0000-0000-0000-000000000003"
  }
}

# A known CloudFront header, for the ephemerality check's positive control.
override_resource {
  target          = random_password.cloudfront_shared_secret
  override_during = plan
  values = {
    result = "synthetic0cloudfront0header0000000000000000000000"
  }
}

variables {
  aws_region      = "af-south-1"
  route53_zone_id = "Z0000000000000000000"
  github_repo     = "Absence0760/project-water-management"
  # Synthetic runtime secrets (ephemeral variables, secrets.tf).
  auth_jwt_secret     = "0123456789abcdef0123456789abcdef0123456789abcdef"
  db_app_password     = "abcdef0123456789abcdef0123456789abcdef01234567"
  alerts_token_secret = "fedcba9876543210fedcba9876543210fedcba9876543210"
  # PEM armour around a placeholder: the shape the variable checks, not a key.
  cloudfront_private_key = "-----BEGIN PRIVATE KEY-----\ntestonlynotakey\n-----END PRIVATE KEY-----"
  # Report downloads (reports.tf): a public key generated for these tests (its
  # private half was never kept). Public keys aren't secret.
  report_download_public_keys = {
    "2026-09" = <<-EOT
      -----BEGIN PUBLIC KEY-----
      MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEAsaz6UBDgol3d4/RdJsAq
      BpgFgFKJjgs+6dXsDOL946Mw+N2ulW4LT/k6DEkGZuCJkef1Lqi0JtuVyjhcEwq8
      hfWgorTT3zNc9JMudwCkUwaqLoPBwYKFai8AhIICqxjlnUm1dmVZTcENYDLF4/aS
      EYLj2o4Wap+A8s+FVItY8Q8Vs4LpqiGI/ue0crfDTSERns+h08kIlp2Q6FKwXy/6
      t/h8nqn67PlOHRqo9I6WmH1yCNuyTwjbVKQLQGA91hLbCaKAw1zbKpPS1kgq+eWr
      qajzJK9XcdRlxS3aSSBU66/3tYik4WNlmrzIopEDCJDkgdwFkNBdFkPesowLlwt9
      nQIDAQAB
      -----END PUBLIC KEY-----
    EOT
  }
  report_download_signing_key = "2026-09"
  # Required and validated. Not an example.* (or other RFC 2606) address,
  # which the variable refuses: the site's own domain, and a plan-only test
  # never subscribes it.
  budget_alert_email = "ops@water-management.jaredhoward.com"
}

run "dlq_alarms_fire_on_new_arrivals" {
  command = plan

  # Every DLQ in infra/, with the queue its alarm must watch.
  assert {
    condition = alltrue([for pair in [
      [aws_cloudwatch_metric_alarm.jobs_dlq_depth, aws_sqs_queue.jobs_dlq.name],
      [aws_cloudwatch_metric_alarm.fetch_requests_dlq_depth, aws_sqs_queue.fetch_requests_dlq.name],
      [aws_cloudwatch_metric_alarm.ingest_results_dlq_depth, aws_sqs_queue.ingest_results_dlq.name],
      [aws_cloudwatch_metric_alarm.render_requests_dlq_depth, aws_sqs_queue.render_requests_dlq.name],
      [aws_cloudwatch_metric_alarm.render_results_dlq_depth, aws_sqs_queue.render_results_dlq.name],
      [aws_cloudwatch_metric_alarm.mail_events_dlq_depth, aws_sqs_queue.mail_events_dlq.name],
      ] : (
      # Metric math, not the raw depth: a depth alarm stays in ALARM for the
      # DLQ's 14-day retention and a second failure never notifies.
      pair[0].metric_name == null &&
      one([for q in pair[0].metric_query : q.expression if q.return_data == true]) == "DIFF(FILL(visible, 0))" &&
      one([for q in pair[0].metric_query : q.id if q.return_data == true]) == "arrivals" &&
      one([for q in pair[0].metric_query : q.metric[0].metric_name if q.id == "visible"]) == "ApproximateNumberOfMessagesVisible" &&
      one([for q in pair[0].metric_query : q.metric[0].namespace if q.id == "visible"]) == "AWS/SQS" &&
      one([for q in pair[0].metric_query : q.metric[0].stat if q.id == "visible"]) == "Maximum" &&
      one([for q in pair[0].metric_query : q.metric[0].period if q.id == "visible"]) == 300 &&
      one([for q in pair[0].metric_query : q.metric[0].dimensions["QueueName"] if q.id == "visible"]) == pair[1] &&
      pair[0].comparison_operator == "GreaterThanThreshold" &&
      pair[0].threshold == 0 &&
      pair[0].evaluation_periods == 3 &&
      pair[0].datapoints_to_alarm == 1 &&
      endswith(pair[0].alarm_name, "-dlq-arrivals") &&
      pair[0].alarm_actions == toset([aws_sns_topic.alerts.arn])
    )])
    error_message = "Every DLQ must alarm on a rise in ApproximateNumberOfMessagesVisible (DIFF(FILL(visible, 0)) > 0, 1 of 3 five-minute datapoints; NumberOfMessagesSent misses redrive moves), to the alerts topic."
  }
}

run "fetcher_and_renderer_failures_alarm" {
  command = plan

  # --- Answered failures: a structured log line, a metric, an alarm -------------
  assert {
    condition = (
      aws_cloudwatch_log_metric_filter.feed_fetch_failed.log_group_name == aws_cloudwatch_log_group.fetcher.name &&
      aws_cloudwatch_log_metric_filter.feed_fetch_failed.pattern == "{ $.message.event = \"feed_fetch_failed\" }" &&
      aws_cloudwatch_metric_alarm.feed_fetch_failed.metric_name == aws_cloudwatch_log_metric_filter.feed_fetch_failed.metric_transformation[0].name &&
      aws_cloudwatch_metric_alarm.feed_fetch_failed.namespace == aws_cloudwatch_log_metric_filter.feed_fetch_failed.metric_transformation[0].namespace &&
      aws_cloudwatch_metric_alarm.feed_fetch_failed.statistic == "Sum" &&
      aws_cloudwatch_metric_alarm.feed_fetch_failed.threshold == 0 &&
      aws_cloudwatch_metric_alarm.feed_fetch_failed.comparison_operator == "GreaterThanThreshold"
    )
    error_message = "A fetch the fetcher answers as failed (lambda-fetcher.ts logs feed_fetch_failed) must alarm: its Errors metric never sees it."
  }
  assert {
    condition = (
      aws_cloudwatch_log_metric_filter.report_render_failed.log_group_name == aws_cloudwatch_log_group.renderer.name &&
      aws_cloudwatch_log_metric_filter.report_render_failed.pattern == "{ $.message.event = \"report_render_failed\" }" &&
      aws_cloudwatch_metric_alarm.report_render_failed.metric_name == aws_cloudwatch_log_metric_filter.report_render_failed.metric_transformation[0].name &&
      aws_cloudwatch_metric_alarm.report_render_failed.namespace == aws_cloudwatch_log_metric_filter.report_render_failed.metric_transformation[0].namespace &&
      aws_cloudwatch_metric_alarm.report_render_failed.statistic == "Sum" &&
      aws_cloudwatch_metric_alarm.report_render_failed.threshold == 0 &&
      aws_cloudwatch_metric_alarm.report_render_failed.comparison_operator == "GreaterThanThreshold"
    )
    error_message = "A render the renderer answers as failed (lambda-renderer.ts logs report_render_failed) must alarm: its Errors metric never sees it."
  }

  # --- Stuck queues ------------------------------------------------------------------
  assert {
    condition = (
      aws_cloudwatch_metric_alarm.fetch_requests_age.metric_name == "ApproximateAgeOfOldestMessage" &&
      aws_cloudwatch_metric_alarm.fetch_requests_age.dimensions["QueueName"] == aws_sqs_queue.fetch_requests.name &&
      aws_cloudwatch_metric_alarm.fetch_requests_age.statistic == "Maximum" &&
      aws_cloudwatch_metric_alarm.fetch_requests_age.threshold == 3600 &&
      aws_cloudwatch_metric_alarm.fetch_requests_age.threshold < aws_sqs_queue.fetch_requests.message_retention_seconds
    )
    error_message = "A fetch request waiting over an hour must alarm, well before it expires."
  }
  # Without renderer_image_tag (this run) the function doesn't exist and render
  # requests have no consumer: the age alarm is exactly what sees that.
  assert {
    condition = (
      length(aws_lambda_function.renderer) == 0 &&
      aws_cloudwatch_metric_alarm.render_requests_age.metric_name == "ApproximateAgeOfOldestMessage" &&
      aws_cloudwatch_metric_alarm.render_requests_age.dimensions["QueueName"] == aws_sqs_queue.render_requests.name &&
      aws_cloudwatch_metric_alarm.render_requests_age.statistic == "Maximum" &&
      aws_cloudwatch_metric_alarm.render_requests_age.threshold == 1800 &&
      aws_cloudwatch_metric_alarm.render_requests_age.threshold < aws_sqs_queue.render_requests.message_retention_seconds
    )
    error_message = "The render-requests age alarm must exist even before the renderer does (an empty renderer_image_tag otherwise lets requests expire silently after 4 days)."
  }

  # --- Throttles ------------------------------------------------------------------------
  assert {
    condition = (
      aws_cloudwatch_metric_alarm.fetcher_throttles.metric_name == "Throttles" &&
      aws_cloudwatch_metric_alarm.fetcher_throttles.namespace == "AWS/Lambda" &&
      aws_cloudwatch_metric_alarm.fetcher_throttles.dimensions["FunctionName"] == aws_lambda_function.fetcher.function_name &&
      aws_cloudwatch_metric_alarm.fetcher_throttles.threshold == 0
    )
    error_message = "Fetcher throttles must alarm: a throttled SQS poll burns a receive toward the DLQ."
  }
  assert {
    condition     = length(aws_cloudwatch_metric_alarm.renderer_throttles) == 0
    error_message = "No renderer, no renderer throttle alarm (its dimension would name a function that doesn't exist)."
  }

  # --- All of them notify the alerts topic ------------------------------------------------
  assert {
    condition = alltrue([for a in [
      aws_cloudwatch_metric_alarm.feed_fetch_failed,
      aws_cloudwatch_metric_alarm.report_render_failed,
      aws_cloudwatch_metric_alarm.fetch_requests_age,
      aws_cloudwatch_metric_alarm.render_requests_age,
      aws_cloudwatch_metric_alarm.fetcher_throttles,
    ] : a.alarm_actions == toset([aws_sns_topic.alerts.arn]) && a.treat_missing_data == "notBreaching"])
    error_message = "Each new alarm notifies the alerts SNS topic, and treats no data (an idle queue or Lambda) as OK."
  }
}

run "renderer_throttles_alarm_once_it_exists" {
  command = plan

  variables {
    renderer_image_tag = "0.4.0-0123456789ab"
  }

  assert {
    condition = (
      aws_cloudwatch_metric_alarm.renderer_throttles[0].metric_name == "Throttles" &&
      aws_cloudwatch_metric_alarm.renderer_throttles[0].namespace == "AWS/Lambda" &&
      aws_cloudwatch_metric_alarm.renderer_throttles[0].dimensions["FunctionName"] == aws_lambda_function.renderer[0].function_name &&
      aws_cloudwatch_metric_alarm.renderer_throttles[0].threshold == 0 &&
      aws_cloudwatch_metric_alarm.renderer_throttles[0].alarm_actions == toset([aws_sns_topic.alerts.arn])
    )
    error_message = "Renderer throttles must alarm once the renderer exists."
  }
}
